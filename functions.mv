<MvCOMMENT>
|
|	Custom Functions per Item
|
|	Function Setup:
|
|	<MvFUNCTION NAME = "XXXXX"  PARAMETERS = "module var, param, param_1, param_2, param_3, etc var" STANDARDOUTPUTLEVEL = "">
|
</MvCOMMENT>

<MvFUNCTION NAME = "Test" PARAMETERS = "module var, param, test_the_first, test_the_second, destination var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.destination" VALUE = "Helllo world">
</MvFUNCTION>

<MvFUNCTION NAME = "Format_Currency" PARAMETERS = "module var, param, plain_price, formatted_price var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Format_Currency( plain_price, formatted_price var )
	|
	</MvCOMMENT>

	<MvDO FILE = "{ g.Module_Root $ g.Store:currncy_mod:module }" NAME = "l.formatted_price" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.plain_price ) }" />
</MvFUNCTION>

<MvFUNCTION NAME = "Upcharge" PARAMETERS = "module var, param, charge_name, charge_type, amount, tax_exempt" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Upcharge( charge_name, charge_type, amount, tax_exempt )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].BasketCharge_Delete_All_Type( g.Basket:basket_id, l.charge_type ) }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'An error occured' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.amount GT 0 }">
		<MvASSIGN NAME = "l.basket_charge:basket_id"	VALUE = "{ g.Basket:basket_id }">
		<MvASSIGN NAME = "l.basket_charge:type"			VALUE = "{ l.charge_type }">
		<MvASSIGN NAME = "l.basket_charge:descrip"		VALUE = "{ l.charge_name }">
		<MvASSIGN NAME = "l.basket_charge:amount"		VALUE = "{ l.amount }">
		<MvASSIGN NAME = "l.basket_charge:disp_amt"		VALUE = "{ l.amount }">
		<MvASSIGN NAME = "l.basket_charge:tax_exempt"	VALUE = "{ l.tax_exempt }">
		<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.sucess" VALUE = "{ BasketCharge_Insert( l.basket_charge ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Refresh the Basket Charges & Basket Total
	|
	</MvCOMMENT>

	<MvFOREACH ITERATOR = "l.charge" ARRAY = "l.charges" COUNT = "{ [ g.Module_Library_DB ].BasketChargeList_Load_Basket( g.Basket:basket_id, l.charges ) }">
		<MvDO FILE = "{ g.Module_Root $ g.Store:currncy_mod:module }" NAME = "l.charge:formatted_amount" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.charge:amount ) }" />
		<MvDO FILE = "{ g.Module_Root $ g.Store:currncy_mod:module }" NAME = "l.charge:formatted_disp_amt" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.charge:disp_amt ) }" />
	</MvFOREACH>
	<MvASSIGN NAME = "l.all_settings:basket:charges" VALUE = "{ l.charges }">

	<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.all_settings:basket:total" VALUE = "{ Basket_Total( g.Basket:basket_id ) }">
	<MvDO FILE = "{ g.Module_Store_Module_Currency }" NAME = "l.all_settings:basket:formatted_total" VALUE = "{ CurrencyModule_AddFormatting( g.Store_Module_Currency, l.all_settings:basket:total ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Variant_Array" PARAMETERS = "module var, param, product_id, variants var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Variant_Array( product_id, variants var )
	|	Yaaaas, Kween.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID( l.product_id, l.loaded_product ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariants"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ProductVariants WHERE product_id = ? GROUP BY variant_id' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-ATT-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.variant_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariants.d.EOF }">
		<MvASSIGN NAME = "l.variant_count"												VALUE = "{ l.variant_count + 1 }">
		
		<MvASSIGN NAME = "l.variants" INDEX = "{ l.variant_count }" MEMBER = "product_id"	VALUE = "{ ProductVariants.d.product_id }">
		<MvASSIGN NAME = "l.variants" INDEX = "{ l.variant_count }" MEMBER = "variant_id"	VALUE = "{ ProductVariants.d.variant_id }">
		<MvASSIGN NAME = "l.variants" INDEX = "{ l.variant_count }" MEMBER = "dimensions"	VALUE = "{ ProductVariants.d.dimensions }">
		<MvASSIGN NAME = "l.variants" INDEX = "{ l.variant_count }" MEMBER = "part_count"	VALUE = "{ ProductVariants.d.part_count }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariants" ROWS = 1>
	</MvWHILE>
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariants">

	<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ Product_Load_ID( l.product_id, l.master_product ) }">

	<MvFOREACH ITERATOR = "l.variant" ARRAY = "l.variants" INDEX = "l.vpos">
		<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ ProductList_Load_Variant( l.variant:product_id, l.variant:variant_id, l.variant:parts ) }">
		<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ ProductVariantList_Load_Variant( l.variant:product_id, l.variant:variant_id, l.variant:attributes ) }">

		<MvASSIGN NAME = "l.variant:overall_inventory" VALUE = "{ l.master_product }">
		<MvDO FILE = "{ g.Module_Feature_INV_RT }" NAME = "l.success" VALUE = "{ Inventory_Load_Variant( l.variant:overall_inventory, l.variant:variant_id ) }">

		<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.variant:attributes">
			<MvASSIGN NAME = "l.attribute:dimensions" VALUE = "">
			<MvASSIGN NAME = "l.attribute:variant_id" VALUE = "">
			<MvASSIGN NAME = "l.attribute:product_id" VALUE = "">
			<MvASSIGN NAME = "l.attribute:part_count" VALUE = "">

			<MvIF EXPR = "{ l.attribute:attmpat_id GT 0 }">
				<MvDO FILE = "{ g.Module_Feature_ATT_DB }" NAME = "l.success" VALUE = "{ AttributeTemplateAttr_Load_ID( l.attribute:attmpat_id, l.attribute:attr ) }">
				<MvDO FILE = "{ g.Module_Feature_ATT_DB }" NAME = "l.success" VALUE = "{ AttributeTemplate_Load_ID( l.attribute:attr:attemp_id, l.attribute:template ) }">
				<MvIF EXPR = "{ l.attribute:option_id GT 0 }">
					<MvDO FILE = "{ g.Module_Feature_ATT_DB }" NAME = "l.success" VALUE = "{ AttributeTemplateOption_Load_ID( l.attribute:option_id, l.attribute:option ) }">
				</MvIF>
			<MvELSE>
				<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ Attribute_Load_ID( l.attribute:attr_id, l.attribute:attr ) }">
				<MvIF EXPR = "{ l.attribute:option_id GT 0 }">
					<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ Option_Load_ID( l.attribute:option_id, l.attribute:option ) }">
				</MvIF>
			</MvIF>
		</MvFOREACH>
	</MvFOREACH>
</MvFUNCTION>


<MvFUNCTION NAME = "Date" PARAMETERS = "module var, param, format, timestamp, return var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Date( format, timestamp, return var )
	|
	</MvCOMMENT>


	<MvCOMMENT>
	|
	| Using some of PHP's Date Formatting
	|
	|	DAY
	|		d, j, N, S, z, l, D
	|
	|	MONTH
	|		m, n, F, M
	|
	|	YEAR
	|		Y, y, L
	|
	|	TIME
	|		a, A, g, G, h, H, i, s
	|
	|	FULL DATE/TIME
	|		c
	|
	</MvCOMMENT>


	<MvASSIGN NAME = "l.time_zone"	VALUE = "{ g.Merchant_Local_Timezone }">

	<MvASSIGN NAME = "l.output" VALUE = "">

	<MvASSIGN NAME = "l.months_long" VALUE = "{ miva_array_deserialize( 'January,February,March,April,May,June,July,August,September,October,November,December' ) }">
	<MvASSIGN NAME = "l.months_short" VALUE = "{ miva_array_deserialize( 'Jan,Feb,Mar,Apr,May,Jun,Jul,Aug,Sept,Oct,Nov,Dec' ) }">

	<MvASSIGN NAME = "l.days_long" VALUE = "{ miva_array_deserialize( 'Sunday,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday' ) }">
	<MvASSIGN NAME = "l.days_short" VALUE = "{ miva_array_deserialize( 'Sun,Mon,Tues,Wed,Thurs,Fri,Sat' ) }">

	<MvFOR INDEX = "l.pos" COUNT = "{ len_var( l.format ) }">
		<MvASSIGN NAME = "l.char" VALUE = "{ substring_var( l.format, l.pos, 1 ) }">

		<MvIF EXPR = "{ NOT ( l.char CIN 'djNSzlDmnFMYyLaAgGhHisc' ) }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.char }">
			<MvFORCONTINUE />
		</MvIF>

		<MvIF EXPR = "{ l.char EQ 'd' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ padl( time_t_dayofmonth( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'j' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ time_t_dayofmonth( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'N' }">
			<MvCOMMENT>
			|
			| (Sunday=1) :|
			|
			</MvCOMMENT>
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ time_t_dayofweek( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'S' }">
			<MvASSIGN NAME = "l.dow" VALUE = "{ time_t_dayofmonth( l.timestamp, l.time_zone ) }">
			<MvASSIGN NAME = "l.dow_a" VALUE = "{ l.dow MOD 10 }">
			<MvASSIGN NAME = "l.dow_b" VALUE = "{ l.dow MOD 100 }">

			<MvIF EXPR = "{ ( l.dow_a EQ 1 ) AND ( l.dow_b NE 11) }">
				<MvASSIGN NAME = "l.S" VALUE = "st">
			<MvELSEIF EXPR = "{ ( l.dow_a EQ 2) AND ( l.dow_b NE 12 ) }">
				<MvASSIGN NAME = "l.S" VALUE = "nd">
			<MvELSEIF EXPR = "{ ( l.dow_a EQ 3 ) AND ( l.dow_b NE 13 ) }">
				<MvASSIGN NAME = "l.S" VALUE = "rd">
			<MvELSE>
				<MvASSIGN NAME = "l.S" VALUE = "th">
			</MvIF>
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.S }">
		<MvELSEIF EXPR = "{ l.char EQ 'z' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ time_t_dayofyear( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'D' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_variable_value( 'l.days_short[ ' $ time_t_dayofweek( l.timestamp, l.time_zone ) $ ' ]') }">
		<MvELSEIF EXPR = "{ l.char EQ 'l' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_variable_value( 'l.days_long[ ' $ time_t_dayofweek( l.timestamp, l.time_zone ) $ ' ]') }">
		<MvELSEIF EXPR = "{ l.char EQ 'm' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ padl( time_t_month( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'n' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ time_t_month( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'F' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_variable_value( 'l.months_long[ ' $ time_t_month( l.timestamp, l.time_zone ) $ ' ]') }">
		<MvELSEIF EXPR = "{ l.char EQ 'M' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_variable_value( 'l.months_short[ ' $ time_t_month( l.timestamp, l.time_zone ) $ ' ]') }">
		<MvELSEIF EXPR = "{ l.char EQ 'Y' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ time_t_year( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'y' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ substring_var( time_t_year( l.timestamp, l.time_zone ), 3, 2) }">
		<MvELSEIF EXPR = "{ l.char EQ 'L' }">
			<MvASSIGN NAME = "l.year" VALUE = "{ time_t_year( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ ( ( ( l.year MOD 400 ) EQ 0 ) OR ( ( l.year MOD 100 ) NE 0 ) ) AND ( ( l.year MOD 4 ) EQ 0 ) }">
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '1' }">
			<MvELSE>
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '0' }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'a' }">
			<MvASSIGN NAME = "l.hour" VALUE = "{ time_t_hour( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ l.hour GT 11 }">
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ 'pm' }">
			<MvELSE>
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ 'am' }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'A' }">
			<MvASSIGN NAME = "l.hour" VALUE = "{ time_t_hour( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ l.hour GT 11 }">
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ 'PM' }">
			<MvELSE>
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ 'AM' }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'g' }">
			<MvASSIGN NAME = "l.hour" VALUE = "{ time_t_hour( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ l.hour GT 12 }">
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ ( l.hour - 12 ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.hour }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'G' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ time_t_hour( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'h' }">
			<MvASSIGN NAME = "l.hour" VALUE = "{ time_t_hour( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ l.hour GT 12 }">
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ padl( ( l.hour - 12 ), 2, 0 ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ padl( l.hour, 2, 0 ) }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'H' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ padl( time_t_hour( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'i' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ padl( time_t_min( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 's' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ padl( time_t_sec( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'c' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $
								padl( time_t_year( l.timestamp, l.time_zone ), 4, '0' )			$
								 padl( time_t_month( l.timestamp, l.time_zone ), 2, '0' ) 		$
								 padl( time_t_dayofmonth( l.timestamp, l.time_zone ), 2, '0' )	$
								 'T' 															$
								 padl( time_t_hour( l.timestamp, l.time_zone ), 2, '0' )		$
								 padl( time_t_min( l.timestamp, l.time_zone ), 2, '0' )			$
								 padl( time_t_sec( l.timestamp, l.time_zone ), 2, '0' )			$
								 'Z' }">
		<MvELSE>
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.char }">
		</MvIF>

	</MvFOR>

	<MvASSIGN NAME = "l.return" VALUE = "{ l.output }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Load_Product" PARAMETERS = "module var, param, options var, product var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>

		Load_Product( options var, product var )
		------------------------------------------------

		Example Use:
		```
		<h2>Component</h2>
		<mvt:assign name="g.options:code" value="'apple'" />
		<mvt:item name="developer_util" param="Load_Product(g.options, g.product_foo)" />
		<hr>
		<mvt:eval expr="glosub(miva_array_serialize(g.product_foo), ',', '<br>')" />
		```

	</MvCOMMENT>

	<MvASSIGN NAME = "l.product" VALUE = "{ _Load_Product( l.options ) }" />
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvFUNCTION NAME = "_Load_Product" PARAMETERS = "options var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>

		_Load_Product( options var, product var )
		------------------------------------------------

		Low-level function with availability for mvt:do
		Helpful for using variables at the `l.` & `l.settings` scopes

		Example Use:
		```
		<h2>mvt:do</h2>
		<mvt:assign name="l.options:code" value="'apple'" />
		<mvt:assign name="l.options:include" value="'runtime,currency,uris,url'" />
		<mvt:do file="g.Module_Root $ 'modules/util/developer_util.mvc'" name="l.product_v2" value="_Load_Product(l.options)" />
		<hr>
		<mvt:eval expr="glosub(miva_array_serialize(l.product_v2), ',', '<br>')" />
		```

	</MvCOMMENT>

	<MvIF EXPR = "{ l.options:id }">
		<MvIF EXPR = "{ 'runtime' IN l.options:include }">
			<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.result" VALUE = "{ Runtime_Product_Load_ID( l.options:id, l.product ) }" />
		<MvELSE>
			<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.result" VALUE = "{ Product_Load_ID( l.options:id, l.product ) }" />
		</MvIF>
	<MvELSEIF EXPR = "{ l.options:code }">
		<MvIF EXPR = "{ 'runtime' IN l.options:include }">
			<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.result" VALUE = "{ Runtime_Product_Load_Code( l.options:code, l.product ) }" />
		<MvELSE>
			<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.result" VALUE = "{ Product_Load_Code( l.options:code, l.product ) }" />
		</MvIF>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>


	<MvIF EXPR = "{ ISNULL l.product:id }">
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>

	<MvCOMMENT>
		| Currency Formatting
	</MvCOMMENT>
	<MvIF EXPR = "{ 'currency' IN l.options:include }">
		<MvDO FILE = "{ g.Module_Store_Module_Currency }" NAME = "l.product:formatted_price" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.product:price ) }" />
		<MvDO FILE = "{ g.Module_Store_Module_Currency }" NAME = "l.product:formatted_cost" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.product:cost ) }" />
	</MvIF>

	<MvCOMMENT>
		| Runtime
	</MvCOMMENT>
	<MvIF EXPR = "{ 'runtime' IN l.options:include }">
		<MvDO FILE = "{ g.Module_Feature_TUI_UT }" NAME = "l.result" VALUE = "{ CommonComponentFields_Initialize_Product_Runtime( l.product ) }" />
	</MvIF>

	<MvCOMMENT>
		| URIs
	</MvCOMMENT>
	<MvIF EXPR = "{ 'uris' CIN l.options:include }">
		<MvDO FILE = "{ g.Module_Feature_URI_DB }" NAME = "l.success" VALUE = "{ URIList_Load_Product( l.product:id, l.product:uris ) }" />
	</MvIF>

	<MvCOMMENT>
		| Store URL
	</MvCOMMENT>
	<MvIF EXPR = "{ 'url' CIN l.options:include }">
		<MvDO FILE = "{ g.Module_Feature_URI_UT }" NAME = "l.product:link" VALUE = "{ Store_Product_URL( l.product, l.null ) }" />
	</MvIF>


	<MvFUNCTIONRETURN VALUE = "{ l.product }" />
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Load_Imagetype_LowLevel" PARAMETERS = "module var, param, product_id, type_id, image_width, image_height" STANDARDOUTPUTLEVEL = "">

	<MvIF EXPR = "{ NOT [g.Module_Library_DB ].ProductImage_Load_Type( l.product_id, l.type_id, l.product_imagetype ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [g.Module_Library_DB ].GeneratedImage_Load_Dimensions( l.product_imagetype:image_id, l.image_width, l.image_height, l.cropped_image ) }">
		<MvIF EXPR = "{ NOT [g.Module_Library_DB ].Image_Load_ID( l.product_imagetype:image_id, l.imagedata ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [g.Module_Library_DB ].Image_Load_File( l.imagedata:image, l.product_image ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [g.Module_Library_DB ].GeneratedImage_FindOrInsert_Image_Dimensions( l.product_image, l.image_width, l.image_height, l.cropped_image ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ l.cropped_image:image }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Load_Imagetype" PARAMETERS = "module var, param, product_id, type_code, image_width, image_height, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Product_Load_Imagetype( product_id, type_code, image_width, image_height, output var )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID( l.product_id, l.loaded_product ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ImageType_Load_Code( l.type_code, l.imagetype ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.output" VALUE = "{ Product_Load_Imagetype_LowLevel( l.module, l.param, l.product_id, l.imagetype:id, l.image_width, l.image_height ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductArray_Load_Imagetype" PARAMETERS = "module var, param, product_array var, type_code, image_width, image_height, member" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	ProductArray_Load_Imagetype( product_array var, type_code, image_width, image_height, member )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ImageType_Load_Code( l.type_code, l.imagetype ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.product" ARRAY = "l.product_array">
		<MvASSIGN NAME = "l.product_imagetype"	VALUE = "">
		<MvASSIGN NAME = "l.imagedata"			VALUE = "">
		<MvASSIGN NAME = "l.product_image"		VALUE = "">
		<MvASSIGN NAME = "l.cropped_image"		VALUE = "">

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID( l.product:id, l.loaded_product ) }">
			<MvFOREACHCONTINUE />
		</MvIF>

		<MvASSIGN NAME = "l.product" MEMBER = "{ l.member }" VALUE = "{ Product_Load_Imagetype_LowLevel( l.module, l.param, l.product:id, l.imagetype:id, l.image_width, l.image_height ) }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "SQL" PARAMETERS = "module var, param, parameters var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	SQL( parameters var )
	|	Taken from Gassan's module
	|
	|	l.parameters:query
	|	l.parameters:bind_parameters (array)
	|
	|	Returns to l.parameters:results
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT len( l.parameters:query ) GT 0 }">
		
		<MvIF EXPR = "{ l.parameters:assign_error EQ 1 }">
			<MvASSIGN NAME  =  "l.parameters:error" VALUE = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE  =  0 />
		<MvELSE>
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'SQL-QUERIES-0002', 'No Query Found Although One Was Expected' ) }">
		</MvIF>
	</MvIF>

	<MvCOMMENT>Determine what type of query was passed</MvCOMMENT>
	<MvASSIGN NAME = "l.parameters:query" VALUE = "{ trim(l.parameters:query) }" />
	<MvIF EXPR = "{ indexofi('SELECT', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "SELECT" />
	<MvELSEIF EXPR = "{ indexofi('INSERT', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "INSERT" />
	<MvELSEIF EXPR = "{ indexofi('UPDATE', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "UPDATE" />
	<MvELSEIF EXPR = "{ indexofi('DELETE', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "DELETE" />
	<MvELSEIF EXPR = "{ indexofi('EXPLAIN', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "EXPLAIN" />
	<MvELSEIF EXPR = "{ indexofi('DROP', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "DROP" />
	<MvELSEIF EXPR = "{ indexofi('CREATE', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "CREATE" />
	<MvELSEIF EXPR = "{ indexofi('ALTER', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "ALTER" />
	<MvELSEIF EXPR = "{ indexofi('SHOW', l.parameters:query, 1) EQ 1 }">
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "SHOW" />
	<MvELSE>
		<MvASSIGN NAME = "l.parameters:query_type" VALUE = "OTHER" />
	</MvIF>

	<MvCOMMENT>Build Prepared Query Bind Parameters</MvCOMMENT>
	<MvIF EXPR = "{ miva_array_elements(l.parameters:bind_parameters) GT 0 AND miva_array_collapse(l.parameters:bind_parameters) }">
		<MvFOREACH ITERATOR = "l.bind_parameter" ARRAY = "l.parameters:bind_parameters" INDEX = "l.index">
			<MvIF EXPR = "{ l.index EQ 1 }">
				<MvASSIGN NAME = "l.parameters:bind_parameter_list" VALUE = "{ 'l.parameters:bind_parameters[' $ l.index $ ']' }" />
			<MvELSE>
				<MvASSIGN NAME = "l.parameters:bind_parameter_list" VALUE = "{ l.parameters:bind_parameter_list $ ', l.parameters:bind_parameters[' $ l.index $ ']' }" />
			</MvIF>
		</MvFOREACH>		
	</MvIF>

	<MvCOMMENT>If the passed parameter structure contains a result set, lets clear it out before loading the new one</MvCOMMENT>
	<MvIF EXPR = "{ miva_array_elements(l.parameters:results) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ miva_array_clear(l.parameters:results, 1, miva_array_elements(l.parameters:results)) }">
	</MvIF>

	<MvCOMMENT>Execute the Query</MvCOMMENT>
	<MvIF EXPR = "{ l.parameters:query_type EQ 'SELECT' OR l.parameters:query_type EQ 'EXPLAIN' OR l.parameters:query_type EQ 'SHOW' }">
		<MvOPENVIEW NAME =  "Merchant" VIEW = "SQL" QUERY = "{ l.parameters:query }" FIELDS =  "{ l.parameters:bind_parameter_list }">

			<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
				<MvIF EXPR = "{ l.parameters:assign_error EQ 1 }">
					<MvASSIGN NAME  =  "l.parameters:error" VALUE = "{ g.MvOPENVIEW_Error }">
					<MvFUNCTIONRETURN VALUE  =  0 />
				<MvELSE>
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'SQL-QUERY-0001', g.MvOPENVIEW_Error ) }">
				</MvIF>
			</MvIF>

			
			<MvCOMMENT>Determine what fields we have from this query result set to build the result array</MvCOMMENT>
			<MvREVEALSTRUCTURE NAME = "MERCHANT" VIEW = "SQL" VARIABLE = "l.fields">

			<MvCOMMENT>Build an array of the result sets in the data structure passed to this function</MvCOMMENT>
			<MvASSIGN NAME = "l.index" VALUE = "1">
			<MvWHILE EXPR = "{ NOT SQL.d.EOF }">
				<MvFOREACH ITERATOR = "l.field" ARRAY = "l.fields" INDEX = "l.field_index">
					<MvASSIGN NAME = "l.parameters:results" INDEX = "{ l.index }" MEMBER = "{ l.field:field_name }" VALUE = "{ miva_variable_value('SQL.d.' $ l.field:field_name) }">
				</MvFOREACH>

				<MvASSIGN NAME = "l.index" VALUE = "{ l.index + 1 }">
				<MvSKIP NAME = "Merchant" VIEW = "SQL" ROWS = 1>
			</MvWHILE>

			<MvASSIGN NAME = "l.parameters:fields" VALUE = "{ l.fields }" />
			
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "SQL">
	<MvELSE>
		<MvTRANSACT NAME = "Merchant">
			<MvQUERY NAME = "Merchant" QUERY = "{ l.parameters:query }" FIELDS = "{ l.parameters:bind_parameter_list }">

			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvROLLBACK NAME = "Merchant">
				<MvIF EXPR = "{ l.parameters:assign_error EQ 1 }">
					<MvASSIGN NAME  =  "l.parameters:error" VALUE = "{ g.MvOPENVIEW_Error }">
					<MvFUNCTIONRETURN VALUE  =  0 />
				<MvELSE>
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'SQL-QUERIES-0001', g.MvQUERY_Error ) }">
				</MvIF>
				
			</MvIF>

			<MvASSIGN NAME = "l.parameters:results" VALUE = "1">
		<MvCOMMIT NAME = "Merchant">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Product_Image" PARAMETERS = "module var, param, category_id, type_code, image_width, image_height, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Category_Product_Image( category_id, type_code, image_width, image_height, output var )
	|	Retrieves an image from the first runtime product from the category.
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.output" VALUE = "">
	<MvASSIGN NAME = "l.offset" VALUE = 0>

	<MvWHILE EXPR = "{ ISNULL l.output }">
		<MvASSIGN NAME = "l.products" VALUE = "">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_ProductList_Load_Offset_Category( l.category_id, l.offset, 1, l.next_offset, l.products ) }">
			<MvWHILESTOP />
		</MvIF>
		<MvIF EXPR = "{ l.offset EQ l.next_offset }">
			<MvWHILESTOP />
		</MvIF>
		<MvIF EXPR = "{ miva_array_elements( l.products ) EQ 0 }">
			<MvWHILESTOP />
		</MvIF>
		<MvASSIGN NAME = "l.offset"			VALUE = "{ l.next_offset }">
		<MvASSIGN NAME = "l.next_offset"	VALUE = "">
		<MvASSIGN NAME = "l.product" VALUE = "{ l.products[1] }">
		<MvASSIGN NAME = "l.success" VALUE = "{ Product_Load_Imagetype( l.module, l.param, l.product:id, l.type_code, l.image_width, l.image_height, l.output ) }">
	</MvWHILE>

</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Output_Variable" PARAMETERS = "module var, param, value var, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	JSON_Output_Variable( value var, output var )
	|	Catpures JSON_Output and saves to a variable.
	|
	</MvCOMMENT>
	<MvCAPTURE VARIABLE = "l.output">
		<MvDO FILE = "{ g.Module_JSON }" NAME = "l.success" VALUE = "{ JSON_Output( l.value ) }">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Output_Floats_Variable" PARAMETERS = "module var, param, value var, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	JSON_Output_Floats_Variable( value var, output var )
	|	Catpures JSON_Output_With_Floats and saves to a variable.
	|
	</MvCOMMENT>
	<MvCAPTURE VARIABLE = "l.output">
		<MvEVAL EXPR = "{ JSON_Output_With_Floats( l.value ) }">
	</MvCAPTURE>
</MvFUNCTION>


<MvFUNCTION NAME = "JSON_Output_With_Floats" PARAMETERS = "value var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCOMMENT>
	|
	|	This is a helper function for JSON_Output_Floats_Variable
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.member_count"	VALUE = "{ miva_struct_members( l.value, l.members ) }">
	<MvIF EXPR = "{ l.member_count GT 0 }">
		{
			<MvFOREACH INDEX = "l.pos" ITERATOR = "l.member" ARRAY = "l.members" COUNT = "{ l.member_count }">
				<MvIF EXPR = "{ l.pos GT 1 }">
					,
				</MvIF>

				<MvREFERENCEARRAY NAME = "l.member_value" VARIABLE = "l.value">
					<MvMEMBER NAME = "{ l.member }">
				</MvREFERENCEARRAY>

				"<MvEVAL EXPR = "{ encodejavascriptstring( l.member ) }">": <MvEVAL EXPR = "{ JSON_Output_With_Floats( l.member_value ) }">
			</MvFOREACH>
		}

		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.element_count"	VALUE = "{ miva_array_elements( l.value ) }">
	<MvIF EXPR = "{ l.element_count GT 0 }">
		[
		<MvFOREACH INDEX = "l.pos" ITERATOR = "l.element" ARRAY = "l.value" COUNT = "{ l.element_count }">
			<MvIF EXPR = "{ l.pos GT 1 }">
				,
			</MvIF>

			<MvEVAL EXPR = "{ JSON_Output_With_Floats( l.element ) }">
		</MvFOREACH>
		]

		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.value_nodec" VALUE = "{ gettoken( l.value, '.', 1 ) }">
	<MvASSIGN NAME = "l.value_negative" VALUE = "{ l.value_nodec * -1 }">

	<MvIF EXPR = "{ ( isdigit( l.value ) OR isdigit( l.value_nodec ) OR isdigit( l.value_negative) ) AND
					NOT ISNULL l.value	AND
					NOT ( len_var( l.value ) GE 2 AND substring_var( l.value, 1, 1 ) EQ '0' ) }">
		<MvEVAL EXPR = "{ l.value }">
	<MvELSE>
		"<MvEVAL EXPR = "{ encodejavascriptstring( l.value ) }">"
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Next_Weekday" PARAMETERS = "module var, param, timestamp, dow, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Next_Weekday( timestamp, dow, output var )
	|	timestamp = starting day in timestamp form
	|	dow = Day Of Week. Sunday = 1, Monday = 2, Etc.
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.timezone" VALUE = "{ g.Merchant_Local_Timezone }">
	<MvASSIGN NAME = "l.day" VALUE = "{ time_t_dayofweek( l.timestamp, l.timezone ) }">

	<MvASSIGN NAME = "l.math" VALUE = "{ ( ( l.day - l.dow ) -7 ) * -1 }">

	<MvASSIGN NAME = "l.output" VALUE = "{ l.timestamp + ( l.math * 86400 ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Page_Render_Variable" PARAMETERS = "module var, param, page_code, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Page_Render_Variable( page_code, output var)
	|	Render a Page Template and save it to a variable.
	|
	</MvCOMMENT>
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.page) }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvCAPTURE VARIABLE = "l.output">
		<MvDO FILE = "{ g.Module_Feature_TUI_MGR }" NAME = "l.page_loaded" VALUE = "{ TemplateManager_Render_Page( l.page_code ) }">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "Recently_Viewed_Products_LowLevel" PARAMETERS = "module var, param, customfield_name, max, current_product_code, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Recently_Viewed_Products_LowLevel( 'recently_viewed', 5, l.settings:product:code, l.settings:recently_viewed:products )
	|
	</MvCOMMENT>
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_Code( 'customfields', l.customfields ) }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvIF EXPR = "{ l.customfields:active EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvDO FILE = "{ g.Module_Root $ l.customfields:module }" NAME = "l.success" VALUE = "{ Read_Basket( l.module, l.param, l.customfield_name, l.recently_viewed:product_codes ) }">

	<MvASSIGN NAME = "l.recently_viewed:product_codes" VALUE = "{ miva_array_deserialize( l.recently_viewed:product_codes ) }">
	
	<MvASSIGN NAME = "l.recently_viewed:exsiting_index" VALUE = "{ miva_array_find( l.current_product_code, l.recently_viewed:product_codes, 0 ) }">

	<MvIF EXPR = "{ l.recently_viewed:exsiting_index GT 0 }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_delete( l.recently_viewed:product_codes, l.recently_viewed:exsiting_index, 1 ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Assign product code array to output var before assigning current product code to the array.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.output" VALUE = "{ l.recently_viewed:product_codes }">

	<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert_var( l.recently_viewed:product_codes, l.current_product_code, 1 ) }">

	<MvASSIGN NAME = "l.recently_viewed:product_count" VALUE = "{ miva_array_elements( l.recently_viewed:product_codes ) }">

	<MvASSIGN NAME = "l.recently_viewed:count_check" VALUE = "{ ( l.recently_viewed:product_count - l.max ) }">

	<MvIF EXPR = "{ l.recently_viewed:count_check GT 0 }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_delete( l.recently_viewed:product_codes, l.max, l.recently_viewed:count_check ) }">
	</MvIF>

	<MvDO FILE = "{ g.Module_Root $ l.customfields:module }" NAME = "l.success" VALUE = "{ Write_Basket( l.module, l.param, l.customfield_name, miva_array_serialize( l.recently_viewed:product_codes) ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Recently_Viewed_Products" PARAMETERS = "module var, param, customfield_name, max, current_product_code, type_code, image_width, image_height, image_member, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Recently_Viewed_Products( 'recently_viewed', 5, l.settings:product:code, 'main', 150, 150, 'main_image', l.settings:recently_viewed )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT Recently_Viewed_Products_LowLevel( l.module, l.param, l.customfield_name, l.max, l.current_product_code, l.products ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ miva_array_elements( l.products ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.product" ARRAY = "l.products">
		<MvASSIGN NAME = "l.options:code" VALUE = "{ l.product }">
		<MvASSIGN NAME = "l.options:include" VALUE = "runtime,currency,url">
		<MvASSIGN NAME = "l.product" VALUE = "{ _Load_Product( l.options ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.success" VALUE = "{ ProductArray_Load_Imagetype( l.module, l.param, l.products, l.type_code, l.image_width, l.image_height, l.image_member ) }">

	<MvASSIGN NAME = "l.output" VALUE = "{ l.products }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodList_ForItems" PARAMETERS = "module var, param, basket var, items var, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	ShippingMethodList_ForItems( g.Basket, l.settings:my_items, l.settings:shipping_methods )
	|
	|	basket var = g.Basket
	|	items var (basically l.settings:basket:items)
	|		Example Structure for each Item:
	|			l.basketitem:product_id
	|			l.basketitem:variant_id	(optional)
	|			l.basketitem:wish_id	(optional)
	|			l.basketitem:subterm_id	(optional)
	|			l.basketitem:code
	|			l.basketitem:name
	|			l.basketitem:retail		(optional)
	|			l.basketitem:base_price	(optional)
	|			l.basketitem:price
	|			l.basketitem:weight
	|			l.basketitem:taxable
	|			l.basketitem:upsold
	|			l.basketitem:quantity
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.temp_basket:cust_id"    VALUE = "{ l.basket:cust_id }" >
	<MvASSIGN NAME = "l.temp_basket:order_id"   VALUE = 0 >
	<MvASSIGN NAME = "l.temp_basket:order_proc" VALUE = 0 >
	<MvASSIGN NAME = "l.temp_basket:ship_id"    VALUE = 0 >
	<MvASSIGN NAME = "l.temp_basket:session_id" VALUE = "{ MakeSessionID() }" >
	<MvASSIGN NAME = "l.temp_basket:lastupdate" VALUE = "{ s.dyn_time_t }" >
	<MvASSIGN NAME = "l.temp_basket:ship_fname" VALUE = "{ l.basket:ship_fname }" >
	<MvASSIGN NAME = "l.temp_basket:ship_lname" VALUE = "{ l.basket:ship_lname }" >
	<MvASSIGN NAME = "l.temp_basket:ship_email" VALUE = "{ l.basket:ship_email }" >
	<MvASSIGN NAME = "l.temp_basket:ship_comp"  VALUE = "{ l.basket:ship_comp }" >
	<MvASSIGN NAME = "l.temp_basket:ship_phone" VALUE = "{ l.basket:ship_phone }" >
	<MvASSIGN NAME = "l.temp_basket:ship_fax"   VALUE = "{ l.basket:ship_fax }" >
	<MvASSIGN NAME = "l.temp_basket:ship_addr1" VALUE = "{ l.basket:ship_addr1 }" >
	<MvASSIGN NAME = "l.temp_basket:ship_addr2" VALUE = "{ l.basket:ship_addr2 }" >
	<MvASSIGN NAME = "l.temp_basket:ship_city"  VALUE = "{ l.basket:ship_city }" >
	<MvASSIGN NAME = "l.temp_basket:ship_state" VALUE = "{ l.basket:ship_state }" >
	<MvASSIGN NAME = "l.temp_basket:ship_zip"   VALUE = "{ l.basket:ship_zip }" >
	<MvASSIGN NAME = "l.temp_basket:ship_cntry" VALUE = "{ l.basket:ship_cntry }" >
	<MvIF EXPR = "{ ISNULL l.temp_basket:ship_state }">
		<MvASSIGN NAME = "l.temp_basket:ship_state" VALUE = "{ l.basket:ship_state_select }">
	</MvIF>

	<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.basket_insert_success" VALUE = "{ Basket_Insert( l.temp_basket ) }">

	<MvIF EXPR = "{ NOT l.basket_insert_success }">
		<MvFUNCTIONRETURN VALUE = 0 >
	<MvELSE>
		<MvFOREACH ITERATOR = "l.item" ARRAY = "l.items">
			<MvASSIGN NAME = "l.item:basket_id" VALUE = "{ l.temp_basket:basket_id }">
			<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ BasketItem_Insert( l.item ) }">
		</MvFOREACH>

		<MvASSIGN NAME = "l.original_basket" VALUE = "{ g.Basket }">
		<MvASSIGN NAME = "g.Basket" VALUE = "{ l.temp_basket }">

		<MvDO FILE = "{ g.Module_Feature_SHP_UT }" NAME = "l.success" VALUE = "{ ShippingMethodList_Load_Basket( l.output ) }">

		<MvASSIGN NAME = "g.Basket" VALUE = "{ l.original_basket }">
		<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ Basket_Reset( l.temp_basket ) }">
	</MvIF>

</MvFUNCTION>


<MvFUNCTION NAME = "Category_Tree_Image" PARAMETERS = "module var, param, category_id, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Category_Tree_Image( l.settings:category:id, l.settings:category:tree_image )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_Code( 'cmp-cssui-cattree', l.cattree ) }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvIF EXPR = "{ l.cattree:active EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvDO FILE = "{ g.Module_Root $ l.cattree:module }" NAME = "l.success" VALUE = "{ CSSUI_CatTree_Load( l.category_id, l.temp_output ) }">
	<MvASSIGN NAME = "l.output" VALUE = "{ l.temp_output:image }">
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Title_Image" PARAMETERS = "module var, param, category_id, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Category_Title_Image( l.settings:category:id, l.settings:category:tree_image )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_Code( 'cmp-cssui-cattitle', l.cattitle ) }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvIF EXPR = "{ l.cattitle:active EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvDO FILE = "{ g.Module_Root $ l.cattitle:module }" NAME = "l.success" VALUE = "{ CSSUI_CatTitle_Load( l.category_id, l.temp_output ) }">
	<MvASSIGN NAME = "l.output" VALUE = "{ l.temp_output:image }">
</MvFUNCTION>

<MvFUNCTION NAME = "Scheduled_Promo_PriceGroup" PARAMETERS = "module var, param, pricegroup_name, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Scheduled_Promo_PriceGroup( pricegroup_name, output var )
	|	output var returns 1 or 0. This will determine if you should display the content.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].PriceGroup_Load_Name( l.pricegroup_name, l.pricegroup) }">
		<MvASSIGN NAME = "l.output" VALUE = 0>
		<MvFUNCTIONRETURN VALUE = 0 />
	</MvIF>

	<MvIF EXPR = "{ ( ( l.pricegroup:dt_start EQ 0 ) OR ( s.dyn_time_t GT l.pricegroup:dt_start) ) AND ( ( l.pricegroup:dt_start EQ 0 ) OR ( s.dyn_time_t LT l.pricegroup:dt_end ) ) }">
		<MvASSIGN NAME = "l.output" VALUE = "1">
		<MvFUNCTIONRETURN VALUE = "1" />
	<MvELSE>
		<MvASSIGN NAME = "l.output" VALUE = 0>
		<MvFUNCTIONRETURN VALUE = 0 />
	</MvIf>	
</MvFUNCTION>

<MvFUNCTION NAME = "Readytheme_Load_ContentSection_NoDiv_Variable" PARAMETERS = "module var, param, code, all_settings var, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Readytheme_Load_ContentSection_NoDiv_Variable( code, all_settings var, output var )
	|	output var returns 1 or 0. This will determine if you should display the content.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_Code( 'readytheme', l.readytheme ) }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvIF EXPR = "{ l.readytheme:active EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvDO FILE = "{ g.Module_Root $ l.readytheme:module }" NAME = "l.load_content_section" VALUE = "{ ReadyTheme_ContentSection_Load_Code( l.code, l.loaded_content_section ) }">

	<MvIF EXPR = "{ ( NOT l.loaded_content_section ) OR ( l.loaded_content_section:active EQ 0 ) }">
		<MvFUNCTIONRETURN VALUE = 0 >
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.loaded_content_section:templ_id, l.content_section ) }">
		<MvCAPTURE VARIABLE = "l.output">
			<MvEVAL EXPR = "{ [ g.Store_Template_Path $ l.content_section:filename ].Template_Render( l.null, l.all_settings ) }">
		</MvCAPTURE>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Readytheme_Load_ContentSection_NoDiv" PARAMETERS = "module var, param, code, all_settings var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Readytheme_Load_ContentSection_NoDiv( code, all_settings var )
	|	output var returns 1 or 0. This will determine if you should display the content.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ Readytheme_Load_ContentSection_NoDiv_Variable( l.module, l.param, l.code, l.all_settings, l.output ) }">
		<MvEVAL EXPR = "{ l.output }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Free_Shipping_Qualifying" PARAMETERS = "module var, param, threshold, basket_total, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Free_Shipping_Qualifying( threshold, basket_total, output var )
	|	output var returns:
	|		qualifies = 1 | 0
	|		remaining = 0.00
	|		formatted_remaining = $0.00
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.determine" VALUE = "{ l.threshold - l.basket_total }">

	<MvIF EXPR = "{ l.determine LE 0 }">
		<MvASSIGN NAME = "l.output" MEMBER = "qualifies" VALUE = "1">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.output" MEMBER = "qualifies" VALUE = 0>
	<MvASSIGN NAME = "l.output" MEMBER = "remaining" VALUE = "{ l.determine }">
	<MvDO FILE = "{ g.Module_Root $ g.Store:currncy_mod:module }" NAME = "l.output:formatted_remaining" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.output:remaining ) }" />
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "Group_By_Member" PARAMETERS = "module var, param, array var, member, results var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Group_By_Member( array var, member, results var )
	|
	</MvCOMMENT>
	<MvFOREACH ITERATOR = "l.item" ARRAY  = "l.array">
		<MvIF EXPR = "{ miva_array_search( l.results, 0, l.result_item, 'l.result_item:member EQ miva_variable_value( \'l.item:\' $ l.member ) ' ) }">
			<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.result_item:groups, l.item, -1 ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.temp:member" VALUE = "{ miva_variable_value( 'l.item:' $ l.member ) }">
			<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.temp:groups, l.item, -1) }">
			<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.results, l.temp, -1) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "Group_By_Structure" PARAMETERS = "module var, param, array var, structure, member, results var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Group_By_Structure( array var, structure, member, results var )
	|
	</MvCOMMENT>

</MvFUNCTION>

<MvFUNCTION NAME = "Hidden_Fields" PARAMETERS = "module var, param, fields" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Hidden_Fields( fields )
	|	This will output hidden fields to the page.
	|	fields = comma seperated global variables
	|		Example:
	|			'g.Super_Awesome,g.Chosen_One,g.Cats_Equal_Love'
	|			'g.Basket' (this would output all the members)
	|
	</MvCOMMENT>
	<MvHIDE FIELDS = "{ l.fields }">
</MvFUNCTION>

<MvFUNCTION NAME = "Send_Email_Attachment_Custom" PARAMETERS = "module var, param, from, to, cc, subject, body_message, file_name, file_dir, file_location, result var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Send_Email_Attachment_Custom( from, to, cc, subject, body_message, file_name, file_dir, file_location, result var )
	|	This works like Send_Email_Attachment, but the body_message is passed.
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.crlf"	VALUE = "{ asciichar( 13 ) $ asciichar( 10 ) }">

	<MvASSIGN NAME = "l.mix_boundary"	VALUE = "{ 'miva-mixed_' $ [ g.Module_Library_Utilities ].GenerateBoundary() }">
	<MvASSIGN NAME = "l.alt_boundary"	VALUE = "{ 'miva-alt_' $ [ g.Module_Library_Utilities ].GenerateBoundary() }">

	<MvASSIGN NAME = "l.mailhost"		VALUE = "{ g.Domain:mailhost }">

	<MvIF EXPR = "{ len( g.Domain:mailmeth ) }">
		<MvIF EXPR = "{ g.Domain:mail_angl }">
			<MvASSIGN NAME = "l.from" 	VALUE = "{ [ g.Module_Library_Utilities ].Email_Add_AngleBrackets( l.from ) }">
			<MvASSIGN NAME = "l.to" 	VALUE = "{ [ g.Module_Library_Utilities ].Email_Add_AngleBrackets( l.to ) }">
			<MvASSIGN NAME = "l.cc" 	VALUE = "{ [ g.Module_Library_Utilities ].Email_Add_AngleBrackets( l.cc ) }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL trim( l.body_message ) }">
			<MvASSIGN NAME = "l.body_message"	VALUE = "{ 'By your request, Miva Merchant has emailed your exported file \'' $ l.file_name $ '\' from your \'' $ g.Store:name $ '\' store.' }">
		</MvIF>

		<MvCAPTURE VARIABLE = "l.headers"><MvASSIGN NAME = "l.ok" VALUE = "{ [ g.Module_Library_Utilities ].BuildMIMEAttachmentEmailHeaders( l.mix_boundary, l.alt_boundary, l.body_message, l.crlf  ) }"></MvCAPTURE>
		<MvCAPTURE VARIABLE = "l.message"><MvASSIGN NAME = "l.ok" VALUE = "{ [ g.Module_Library_Utilities ].BuildMIMEAttachmentEmailMessage( l.mix_boundary, l.file_name, l.file_dir, l.file_location, l.crlf ) }"></MvCAPTURE>
	
		<MIVA MvCOMMERCE_ERROR = "nonfatal, nodisplay">
		<MvCOMMERCE METHOD = "{ g.Domain:mailmeth }" FIELDS = "to,from,cc,subject,headers,message,mailhost">
		</MvCOMMERCE>

		<MvIF EXPR = "{ g.MvCOMMERCE_ERROR }">
			<MvASSIGN NAME = "l.result:error" VALUE = "1">
			<MvASSIGN NAME = "l.result:message" VALUE = "{ g.MvCOMMERCE_ERROR }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PUB-00017', g.MvCOMMERCE_ERROR ) }">
		</MvIF>
	<MvELSE>
		<MIVA MvSMTP_Error = "nonfatal, nodisplay">

		<MvASSIGN NAME = "l.null"				VALUE = "{ [ g.Module_Library_Utilities ].Create_Address_Format( l.from, l.from_header, l.from_address ) }">
		<MvASSIGN NAME = "l.null" 				VALUE = "{ [ g.Module_Library_Utilities ].Create_Address_Formats( l.to, l.to_headers, l.to_addresses ) }">
		<MvASSIGN NAME = "l.null"				VALUE = "{ [ g.Module_Library_Utilities ].Create_Address_Formats( l.cc, l.cc_headers, l.cc_addresses ) }">
		
		<MvIF EXPR = "{ g.Domain:mail_angl }">
			<MvASSIGN NAME = "l.from_address" 	VALUE = "{ [ g.Module_Library_Utilities ].Email_Add_AngleBrackets( l.from_address ) }">
			<MvASSIGN NAME = "l.to_addresses" 	VALUE = "{ [ g.Module_Library_Utilities ].Email_Add_AngleBrackets( l.to_addresses ) }">
			<MvASSIGN NAME = "l.cc_addresses" 	VALUE = "{ [ g.Module_Library_Utilities ].Email_Add_AngleBrackets( l.cc_addresses ) }">
		</MvIF>

		<MvASSIGN NAME = "l.miva_headers" 		VALUE = "{ [ g.Module_Library_Utilities].Create_Miva_Email_Headers( l.from_header, l.to_headers, l.cc_headers, l.subject ) }">
		<MvASSIGN NAME = "l.port" 				VALUE = "{ g.Domain:mailport }">
		<MvASSIGN NAME = "l.username" 			VALUE = "{ g.Domain:mailuser }">
		<MvASSIGN NAME = "l.password" 			VALUE = "{ [ g.Module_Library_Crypto ].Decrypt_Payment_Password( g.Domain:mailpass ) }">
		
		<MvIF EXPR = "{ NOT ISNULL g.Domain:mailflags }">
			<MvASSIGN NAME = "l.flags" 			VALUE = "{ g.Domain:mailflags $ ', noheaders' }">
		<MvELSE>
			<MvASSIGN NAME = "l.flags" 			VALUE = "noheaders">
		</MvIF>

		<MvSMTP FROM 	 = "{ l.from_address }" 
				TO 		 = "{ l.to_addresses }" 
				CC 		 = "{ l.cc_addresses }" 
				SUBJECT  = "{ l.subject }" 
				MAILHOST = "{ l.mailhost }"
				FLAGS 	 = "{ l.flags }"
				PORT 	 = "{ l.port }"
				USERNAME = "{ l.username }"
				PASSWORD = "{ l.password }">
			<MvEVAL EXPR = "{ l.miva_headers }">

			<MvASSIGN NAME = "l.ok" 			VALUE = "{ [ g.Module_Library_Utilities ].BuildMIMEAttachmentEmailHeaders( l.mix_boundary, l.alt_boundary, l.body_message, l.crlf ) }">
			<MvASSIGN NAME = "l.ok" 			VALUE = "{ [ g.Module_Library_Utilities ].BuildMIMEAttachmentEmailMessage( l.mix_boundary, l.file_name, l.file_dir, l.file_location, l.crlf ) }">
		</MvSMTP>

		<MvIF EXPR = "{ g.MvSMTP_Error }">
			<MvASSIGN NAME = "l.result:error" VALUE = "1">
			<MvASSIGN NAME = "l.result:message" VALUE = "{ g.MvSMTP_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PUB-00018', g.MvSMTP_Error ) }">
		</MvIF>
	</MvIF>
	<MvASSIGN NAME = "l.result:success" VALUE = "1">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvFUNCTION NAME = "Products_Also_Purchased_Query" PARAMETERS = "module var, param, product_id, max, type_code, image_width, image_height, image_member, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Products_Also_Purchased_Query( product_id, max, type_code, image_width, image_height, image_member, output var )
	|	This is used to pull Directly from sXX_Orders/ sXX_OrderItems.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID( l.product_id, l.loaded_product ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Also_Purchased"
				QUERY	= "{ '	SELECT
									product_id
								FROM
									' $ g.Store_Table_Prefix $ 'OrderItems
								WHERE
									order_id IN (SELECT order_id FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE product_id = ?  ORDER BY order_id DESC)
								AND
									product_id <> ?
								GROUP BY
									product_id
								LIMIT ?' }"
				FIELDS	= "l.product_id, l.product_id, l.max">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-ATT-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.output:count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Also_Purchased.d.EOF }">
		<MvASSIGN NAME = "l.output:count"												VALUE = "{ l.output:count + 1 }">
		
		<MvASSIGN NAME = "l.output:products" INDEX = "{ l.output:count }" MEMBER = "id"	VALUE = "{ Also_Purchased.d.product_id }">

		<MvSKIP NAME = "Merchant" VIEW = "Also_Purchased" ROWS = 1>
	</MvWHILE>
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Also_Purchased">

	<MvIF EXPR = "{ miva_array_elements( l.output:products ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.product" ARRAY = "l.output:products">
		<MvASSIGN NAME = "l.options:id" VALUE = "{ l.product:id }">
		<MvASSIGN NAME = "l.options:include" VALUE = "runtime,currency,url">
		<MvASSIGN NAME = "l.product" VALUE = "{ _Load_Product( l.options ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.success" VALUE = "{ ProductArray_Load_Imagetype( l.module, l.param, l.output:products, l.type_code, l.image_width, l.image_height, l.image_member ) }">

	<MvASSIGN NAME = "l.output" VALUE = "{ l.output:products }">
</MvFUNCTION>

<MvFUNCTION NAME = "Create_Customer_Email_Password" PARAMETERS = "module var, param, email, password, password_confirm, messages var, redirect_to" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Create_Customer_Email_Password( email, password, password_confirm, messages var, redirect_to )
	|		-> Need a better name for this function.
	|		-> Redirect_to = URL ( e.g. l.settings:urls:ACLN:secure ). If blank, no redirection.
	|		-> messages var returns `error_messages` and `information_messages`
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.email"				VALUE = "{ trim( l.email ) }">
	<MvASSIGN NAME = "l.password"			VALUE = "{ trim( l.password ) }">
	<MvASSIGN NAME = "l.password_confirm"	VALUE = "{ trim( l.password_confirm ) }">
	<MvASSIGN NAME = "l.redirect_to"		VALUE = "{ trim( l.redirect_to ) }">


	<MvIF EXPR = "{ ( ISNULL l.email ) OR ( ISNULL l.password ) OR ( ISNULL l.password_confirm ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.messages:error_messages, 'Please fill in all required fields for creating a new account.', -1 ) }">
		<MvASSIGN NAME = "l.messages:error_message_count" VALUE = "{ l.messages:error_message_count + 1 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( l.email ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.messages:error_messages, 'Please make sure that your email address is a valid email.', -1 ) }">
		<MvASSIGN NAME = "l.messages:error_message_count" VALUE = "{ l.messages:error_message_count + 1 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.password NE l.password_confirm }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.messages:error_messages, 'The passwords do not match.', -1 ) }">
		<MvASSIGN NAME = "l.messages:error_message_count" VALUE = "{ l.messages:error_message_count + 1 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_CUS_DB ].Customer_Load_Email( l.email, l.customer_exsists ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.messages:error_messages, 'The email address you entered is already in use.', -1 ) }">
		<MvASSIGN NAME = "l.messages:error_message_count" VALUE = "{ l.messages:error_message_count + 1 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.new_customer" MEMBER = "login"			VALUE = "{ l.email }">
	<MvASSIGN NAME = "l.new_customer" MEMBER = "pw_email"		VALUE = "{ l.email }">
	<MvASSIGN NAME = "l.new_customer" MEMBER = "password"		VALUE = "{ l.password }">
	<MvASSIGN NAME = "l.new_customer" MEMBER = "pgrpcount"		VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_UT ].CustomerLogin_Generate_Email( l.new_customer:pw_email, l.new_customer:login ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.messages:error_messages, 'An error has occured.', -1 ) }">
		<MvASSIGN NAME = "l.messages:error_message_count" VALUE = "{ l.messages:error_message_count + 1 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].Customer_Insert( l.new_customer ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.messages:error_messages, 'An error has occured.', -1 ) }">
		<MvASSIGN NAME = "l.messages:error_message_count" VALUE = "{ l.messages:error_message_count + 1 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "g.Customer_LoginEmail"	VALUE = "{ l.new_customer:pw_email }">
	<MvASSIGN NAME = "g.Customer_Password"		VALUE = "{ l.new_customer:password }">

	<MvDO FILE = "{ g.Module_Feature_CUS_RT }" NAME = "l.success" VALUE = "{ Action_Customer_Login() }">

	<MvIF EXPR = "{ NOT ISNULL l.redirect_to }">
		<MvASSIGN NAME = "l.status" VALUE = "{ miva_output_header( 'Location', l.redirect_to ) }">
	</MvIF>

	<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert( l.messages:information_messages, 'You have successfully created an account!', -1 ) }">
	<MvASSIGN NAME = "l.messages:information_message_count" VALUE = "{ l.messages:information_message_count + 1 }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReOrder_Order_Form" PARAMETERS = "module var, param, orderitems var, show_quantity_inputs, member" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	ReOrder_Order_Form( orderitems var, show_quantity_inputs, member )
	|	This function will assign all the hidden inputs needed to re-order the order to a specific member.
	|	This will basically create an ADPM form, without the `<form>` tags wrapping it.
	|
	|	This will not work with Variant Basket Replace (module).
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.counter" VALUE = 0 />

	<MvFOREACH ITERATOR = "l.item" ARRAY = "l.orderitems">
		<MvIF EXPR = "{ l.item:reorder NE 1 }">
			<MvFOREACHCONTINUE />
		</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
		<MvASSIGN NAME = "l.option_counter" VALUE = 0>

		<MvASSIGN NAME = "l.output" VALUE = "{ '<input type="hidden" name="Products[' $ l.counter $ ']:code" value="' $ l.item:code $ '" />' }">

		<MvFOREACH ITERATOR = "l.option" ARRAY = "l.item:options">
			<MvASSIGN NAME = "l.option_counter" VALUE = "{ l.option_counter + 1 }">
			<MvIF EXPR = "{ l.option:option_id GT 0 }">
				<MvASSIGN NAME = "l.output" VALUE = "{ 	l.output $ '
														<input type="hidden" name="Products[' $ l.counter $ ']:attributes[' $ l.option_counter $ ']:code" value="' $ l.option:attr_code $ '" />
														<input type="hidden" name="Products[' $ l.counter $ ']:attributes[' $ l.option_counter $ ']:value" value="' $ l.option:opt_code $ '" />' }">
			<MvELSEIF EXPR = "{ NOT ISNULL l.option:data }">
				<MvIF EXPR = "{ ( l.option:attr_code EQ 'digitaldownload' ) AND ( NOT ISNULL l.option:digital_download_url ) }">
					<MvASSIGN NAME = "l.output" VALUE = "{ 	l.output $ '
															<input type="hidden" name="Products[' $ l.counter $ ']:attributes[' $ l.option_counter $ ']:code" value="' $ l.option:attr_code $ '" />
															<input type="hidden" name="Products[' $ l.counter $ ']:attributes[' $ l.option_counter $ ']:value" value="' $ l.option:data $ '" />' }">
				</MvIF>
			<MvELSEIF EXPR = "{ NOT ISNULL l.option:data_long }">
				<MvASSIGN NAME = "l.output" VALUE = "{ 	l.output $ '
														<input type="hidden" name="Products[' $ l.counter $ ']:attributes[' $ l.option_counter $ ']:code" value="' $ l.option:attr_code $ '" />
														<input type="hidden" name="Products[' $ l.counter $ ']:attributes[' $ l.option_counter $ ']:value" value="' $ l.option:data_long $ '" />' }">
			<MvELSE>
				<MvASSIGN NAME = "l.output" VALUE = "{ 	l.output $ '
														<input type="hidden" name="Products[' $ l.counter $ ']:attributes[' $ l.option_counter $ ']:code" value="' $ l.option:attr_code $ '" />
														<input type="hidden" name="Products[' $ l.counter $ ']:attributes[' $ l.option_counter $ ']:value" value="1" />' }">
			</MvIF>
		</MvFOREACH>

		<MvIF EXPR = "{ l.show_quantity_inputs EQ 1 }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '<input type="text" name="Products[' $ l.counter $ ']:quantity" value="' $ l.item:quantity $ '" />' }">
		<MvELSE>
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '<input type="hidden" name="Products[' $ l.counter $ ']:quantity" value="' $ l.item:quantity $ '" />' }">
		</MvIF>

		<MvASSIGN NAME = "l.item" MEMBER = "{ l.member }" VALUE = "{ l.output }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "Replace_Special_Characters" PARAMETERS = "module var, param, input_string, output_string var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Replace_Special_Characters( input_string, outputstring var )
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.special_characters" VALUE = "{ miva_array_deserialize(',,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,') }">
	<MvASSIGN NAME = "l.replace_characters" VALUE = "{ miva_array_deserialize('A,a,A,A,a,A,a,A,a,A,a,A,a,AE,ae,C,c,D,d,E,e,E,e,E,e,E,e,I,i,I,i,I,i,I,i,N,n,O,o,O,o,O,o,O,o,O,o,O,o,s,T,t,U,u,U,u,U,u,U,u,Y,y,y') }">
	<MvASSIGN NAME = "l.output_string" VALUE = "{ glosub_array( l.input_string, l.special_characters, l.replace_characters ) }">

</MvFUNCTION>

<MvFUNCTION NAME = "Product_Images_Srcset" PARAMETERS = "module var, param, product_id, type_code, image_width, image_height, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Product_Images_Srcset( product_id, type_code, image_width, image_height, output var )
	|	-> Create srcsets from the width x height
	| 	-> size entered will be smallest (1x)
	|	-> x2, x3, x4.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID( l.product_id, l.loaded_product ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ImageType_Load_Code( l.type_code, l.imagetype ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.output" VALUE = "">

	<MvASSIGN NAME = "l.dimensions" MEMBER = "scale" INDEX = "1" VALUE = "1x">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "scale" INDEX = "2" VALUE = "2x">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "scale" INDEX = "3" VALUE = "3x">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "scale" INDEX = "4" VALUE = "4x">

	<MvASSIGN NAME = "l.dimensions" MEMBER = "width" INDEX = "1" VALUE = "{ l.image_width }">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "width" INDEX = "2" VALUE = "{ l.image_width * 2 }">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "width" INDEX = "3" VALUE = "{ l.image_width * 3 }">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "width" INDEX = "4" VALUE = "{ l.image_width * 4 }">

	<MvASSIGN NAME = "l.dimensions" MEMBER = "height" INDEX = "1" VALUE = "{ l.image_height }">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "height" INDEX = "2" VALUE = "{ l.image_height * 2 }">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "height" INDEX = "3" VALUE = "{ l.image_height * 3 }">
	<MvASSIGN NAME = "l.dimensions" MEMBER = "height" INDEX = "4" VALUE = "{ l.image_height * 4 }">

	<MvFOREACH ITERATOR = "l.dimension" ARRAY="l.dimensions">
		<MvASSIGN NAME = "l.dimension" MEMBER = "image" VALUE = "{ Product_Load_Imagetype_LowLevel( l.module, l.param, l.product_id, l.imagetype:id, l.dimension:width, l.dimension:height ) }">
		<MvIF EXPR = "{ l.dimension:image }">
			<MvIF EXPR = "{ ISNULL l.output:src }">
				<MvASSIGN NAME = "l.output:src" VALUE = "{ l.dimension:image }">
			</MvIF>
			<MvIF EXPR = "{ l.output:srcset }">
				<MvASSIGN NAME = "l.output:srcset" VALUE = "{ l.output:srcset $ ', ' }">
			</MvIF>
			<MvASSIGN NAME = "l.output:srcset" VALUE = "{ l.output:srcset $ l.dimension:image $ ' ' $ l.dimension:scale }">
		</MvIF>
	</MvFOREACH>

</MvFUNCTION>

<MvFUNCTION NAME = "HarlemShake" PARAMETERS = "module var, param" STANDARDOUTPUTLEVEL = "text,html">
	<MvCOMMENT>
	|
	|	HarlemShake()
	|	Just because.
	|
	</MvCOMMENT>
	<MvEVAL EXPR = "{ '
		<script type="text/javascript">javascript:(function(){function c(){var e=document.createElement("link");e.setAttribute("type","text/css");e.setAttribute("rel","stylesheet");e.setAttribute("href",f);e.setAttribute("class",l);document.body.appendChild(e)}function h(){var e=document.getElementsByClassName(l);for(var t=0;t<e.length;t++){document.body.removeChild(e[t])}}function p(){var e=document.createElement("div");e.setAttribute("class",a);document.body.appendChild(e);setTimeout(function(){document.body.removeChild(e)},100)}function d(e){return{height:e.offsetHeight,width:e.offsetWidth}}function v(i){var s=d(i);return s.height>e&&s.height<n&&s.width>t&&s.width<r}function m(e){var t=e;var n=0;while(!!t){n+=t.offsetTop;t=t.offsetParent}return n}function g(){var e=document.documentElement;if(!!window.innerWidth){return window.innerHeight}else if(e&&!isNaN(e.clientHeight)){return e.clientHeight}return 0}function y(){if(window.pageYOffset){return window.pageYOffset}return Math.max(document.documentElement.scrollTop,document.body.scrollTop)}function E(e){var t=m(e);return t>=w&&t<=b+w}function S(){var e=document.createElement("audio");e.setAttribute("class",l);e.src=i;e.loop=false;e.addEventListener("canplay",function(){setTimeout(function(){x(k)},500);setTimeout(function(){N();p();for(var e=0;e<O.length;e++){T(O[e])}},15500)},true);e.addEventListener("ended",function(){N();h()},true);e.innerHTML=" <p>If you are reading this, it is because your browser does not support the audio element. We recommend that you get a new browser.</p> <p>";document.body.appendChild(e);e.play()}function x(e){e.className+=" "+s+" "+o}function T(e){e.className+=" "+s+" "+u[Math.floor(Math.random()*u.length)]}function N(){var e=document.getElementsByClassName(s);var t=new RegExp("\\b"+s+"\\b");for(var n=0;n<e.length;){e[n].className=e[n].className.replace(t,"")}}var e=30;var t=30;var n=350;var r=350;var i="//s3.amazonaws.com/moovweb-marketing/playground/harlem-shake.mp3";var s="mw-harlem_shake_me";var o="im_first";var u=["im_drunk","im_baked","im_trippin","im_blown"];var a="mw-strobe_light";var f="//s3.amazonaws.com/moovweb-marketing/playground/harlem-shake-style.css";var l="mw_added_css";var b=g();var w=y();var C=document.getElementsByTagName("*");var k=null;for(var L=0;L<C.length;L++){var A=C[L];if(v(A)){if(E(A)){k=A;break}}}if(A===null){console.warn("Could not find a node of the right size. Please try a different page.");return}c();S();var O=[];for(var L=0;L<C.length;L++){var A=C[L];if(v(A)){O.push(A)}}})()</script>
		' }">
</MvFUNCTION>

<MvFUNCTION NAME = "Assign_Member" PARAMETERS = "module var, param, variable var, member, value" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Assign_Member( variable var, member, value )
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.variable" MEMBER = "{ l.member }" VALUE = "{ l.value }">
</MvFUNCTION>

<MvFUNCTION NAME = "Assign_Index" PARAMETERS = "module var, param, array var, index, value" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Assign_Index( array var, index, value )
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.array" INDEX = "{ l.index }" VALUE = "{ l.value }">
</MvFUNCTION>

<MvFUNCTION NAME = "Parse_URI" PARAMETERS = "module var, param, uri, return var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Parse_URI( uri, return var )
	|	scheme, host, user, pass, path, query, fragment, subdomain, domain, tld
	|	=> https://www.freeformatter.com/url-parser-query-string-splitter.html
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.original_uri" VALUE = "{ l.uri }" />
	<MvASSIGN NAME = "l.original_uri_length" VALUE = "{ len_var( l.original_uri ) }">

	<MvCOMMENT>
	|
	|	Check for http:// and https:// and //
	|
	</MvCOMMENT>
	
	<MvIF EXPR = "{ 'HTTP://' EQ toupper( substring_var( l.original_uri, 0, 7 ) ) }">
		<MvASSIGN NAME = "l.return" MEMBER = "scheme" VALUE = "http">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.original_uri, 8, ( l.original_uri_length - 7 ) ) }">
	<MvELSEIF EXPR = "{ 'HTTPS://' EQ toupper( substring_var( l.original_uri, 0, 8 ) ) }">
		<MvASSIGN NAME = "l.return" MEMBER = "scheme" VALUE = "https">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.original_uri, 9, ( l.original_uri_length - 8 ) ) }">
	<MvELSEIF EXPR = "{ '//' EQ substring_var( l.original_uri, 0, 2 ) }">
		<MvASSIGN NAME = "l.return" MEMBER = "scheme" VALUE = "//">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.original_uri, 3, ( l.original_uri_length - 2 ) ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Check for user:pass@ for auth.
	|	Did you know the word aapstert is Afrikaans for Monkey Tail, and that is what they call the @ sign.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.aapstert_start" VALUE = "{ indexof( '@', l.original_uri, 1 ) }">

	<MvIF EXPR = "{ l.aapstert_start GT 0 }">
		<MvASSIGN NAME = "l.return" MEMBER = "auth" VALUE = "{ substring_var( l.original_uri, 1, l.aapstert_start - 1 ) }">
		<MvASSIGN NAME = "l.original_uri_length" VALUE = "{ len_var( l.original_uri ) }">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.original_uri, l.aapstert_start + 1, ( l.original_uri_length - l.aapstert_start ) ) }">

		<MvASSIGN NAME = "l.success" VALUE = "{ miva_splitstring( l.return:auth, ':', l.split_auth, 'trim' ) }">
		<MvASSIGN NAME = "l.return" MEMBER = "user" VALUE = "{ l.split_auth[1] }">
		<MvASSIGN NAME = "l.return" MEMBER = "pass" VALUE = "{ l.split_auth[2] }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Check for /
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.slash_start" VALUE = "{ indexof( '/', l.original_uri, 1 ) }">
	<MvIF EXPR = "{ NOT indexof( '/', l.original_uri, 1 ) }">
		<MvASSIGN NAME = "l.original_uri" VALUE  = "{ l.original_uri $ '/' }">
		<MvASSIGN NAME = "l.slash_start" VALUE = "{ indexof( '/', l.original_uri, 1 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.return" MEMBER = "host" VALUE = "{ substring_var( l.original_uri, 1, ( l.slash_start - 1 ) ) }">

	<MvCOMMENT>
	|
	|	Check for subdomains / domain.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.period_first" VALUE = "{ indexof( '.', l.return:host, 1 ) }">
	<MvASSIGN NAME = "l.period_last" VALUE = "{ indexofl( '.', l.return:host, len_var( l.return:host ) ) }">

	<MvIF EXPR = "{ l.period_first NE l.period_last }">
		<MvASSIGN NAME = "l.return" MEMBER = "subdomain" VALUE = "{ substring_var( l.return:host, 1, l.period_first -1 ) }">
		<MvASSIGN NAME = "l.return" MEMBER = "domain" VALUE  = "{ substring_var( l.return:host, l.period_first + 1, len_var( l.return:host ) - l.period_first ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.return" MEMBER = "domain" VALUE  = "{ l.return:host }">
	</MvIF>
	
	<MvASSIGN NAME = "l.period_first" VALUE = "{ indexofl( '.', l.return:host, len_var( l.return:host ) ) }">
	<MvASSIGN NAME = "l.return" MEMBER = "tld" VALUE = "{ substring_var( l.return:host, l.period_last + 1, ( len_var( l.return:host ) - l.period_last ) ) }">

	<MvASSIGN NAME = "l.host_length" VALUE = "{ len_var( l.return:host ) }">

	<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.original_uri, l.host_length + 2, ( l.original_uri_length - l.host_length + 2 ) ) }">

	<MvASSIGN NAME = "l.original_uri_length" VALUE = "{ len_var( l.original_uri ) }">

	<MvCOMMENT>
	|
	|	Check for #
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.hash_start" VALUE = "{ indexof( '#', l.original_uri, 1 ) }">

	<MvIF EXPR = "{ l.hash_start GT 0 }">
		<MvASSIGN NAME = "l.return" MEMBER = "fragment" VALUE = "{ substring_var( l.original_uri, l.hash_start + 1, ( l.original_uri_length - l.hash_start + 1 ) ) }">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.original_uri, 1, l.hash_start - 1 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query_start" VALUE = "{ indexof( '?', l.original_uri, 1 ) }">

	<MvIF EXPR = "{ l.query_start GT 0 }">
		<MvASSIGN NAME = "l.original_uri_length" VALUE = "{ len_var( l.original_uri ) }">
		<MvASSIGN NAME = "l.return" MEMBER = "path" VALUE = "{ '/' $ substring_var( l.original_uri, 1, l.query_start - 1 ) }">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.original_uri, l.query_start, l.original_uri_length - 1 ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.return" MEMBER = "path" VALUE = "{ l.original_uri }">
	</MvIF>
	
	<MvIF EXPR = "{ NOT ISNULL l.original_uri }">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ glosub( l.original_uri, '?&', '' ) }">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ glosub( l.original_uri, '?', '' ) }">

		<MvASSIGN NAME = "l.return" MEMBER = "query" VALUE = "{ l.original_uri }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.return:path }">
		<MvASSIGN NAME = "l.return" MEMBER = "path" VALUE = "/" />
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URI_QueryStrings_To_Structure" PARAMETERS = "module var, param, uri, array, return var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	URI_QueryStrings_To_Structure( uri, array, return var )
	|		If Query string is empty, it will return with a space... need to find a better way for this.
	|	array = 1 | 0
	|	array = 1 will keep the order of the query strings.
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.success" VALUE = "{ Parse_URI( l.module, l.param, l.uri, l.parsed_uri ) }">

	<MvIF EXPR = "{ ISNULL l.parsed_uri:query }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.query" ARRAY = "l.querys" COUNT = "{ miva_splitstring( l.parsed_uri:query, '&', l.querys, 'trim' ) }">
		<MvASSIGN NAME = "l.member" VALUE = "{ gettoken( l.query, '=', 1 ) }">

		<MvIF EXPR = "{ ISNULL l.member }">
			<MvFOREACHCONTINUE />
		</MvIF>

		<MvASSIGN NAME = "l.query_value" VALUE = "{ gettoken( l.query, '=', 2 ) }">

		<MvIF EXPR = "{ ISNULL l.query_value }">
			<MvASSIGN NAME = "l.query_value" VALUE = " " />
		</MvIF>
		<MvIF EXPR = "{ l.array EQ 1 }">
			<MvASSIGN NAME = "l.return" INDEX = "{ ++l.index }" MEMBER = "{ l.member }" VALUE = "{ l.query_value }">
		<MvELSE>
			<MvASSIGN NAME = "l.return" MEMBER = "{ l.member }" VALUE = "{ l.query_value }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URI_Add_Replace_QueryString" PARAMETERS = "module var, param, uri, query_string, value, return var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	URI_Add_Replace_QueryString( uri, query_string, value, return var )
	|
	</MvCOMMENT>
	<MvIF EXPR = "{ NOT URI_QueryStrings_To_Structure( l.module, l.param, l.uri, 1, l.output ) }">
		<MvASSIGN NAME = "l.return" VALUE = "{ l.uri }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.uri_qm_index" VALUE = "{ indexof( '?&', l.uri, 1 ) }">

	<MvIF EXPR = "{ l.uri_qm_index GT 0 }">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.uri, 0, l.uri_qm_index + 1 ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.uri_qm_index" VALUE = "{ indexof( '?', l.uri, 1 ) }">
		<MvASSIGN NAME = "l.original_uri" VALUE = "{ substring_var( l.uri, 0, l.uri_qm_index ) }">
	</MvIF>

	<MvASSIGN NAME = "l.value" VALUE = "{ glosub( l.value, ' ', '%20' ) }">

	<MvIF EXPR = "{ miva_array_search( l.output, 0, l.q, 'miva_member_exists( l.q, l.query_string )' ) }">
		<MvASSIGN NAME = "l.q" MEMBER = "{ l.query_string }" VALUE = "{ l.value }">
	<MvELSE>
		<MvASSIGN NAME = "l.temp" MEMBER = "{ l.query_string }" VALUE = "{ l.value }">
		<MvASSIGN NAME = "l.success" VALUE = "{ miva_array_insert_var( l.output, l.temp, -1 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.return" VALUE = "">

	<MvFOREACH ITERATOR = "l.query" ARRAY = "l.output">

		<MvIF EXPR = "{ NOT ISNULL l.return }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ '&' }">
		</MvIF>

		<MvASSIGN NAME = "l.success" VALUE = "{ miva_struct_members( l.query, l.query_members ) }">
		<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ l.query_members[1] $ '=' $  miva_variable_value( 'l.query:' $ l.query_members[1] ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.return" VALUE = "{ l.original_uri $ l.return }">
</MvFUNCTION>

<MvFUNCTION NAME = "URI_Build_URI" PARAMETERS = "module var, param, uri var, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	URI_Build_URI( uri var, output var )
	|	scheme, user, pass, subdomain, domain, path, query, fragment
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.output" VALUE = "">

	<MvIF EXPR = "{ NOT ISNULL l.uri:scheme }">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.uri:scheme }" />
		<MvIF EXPR = "{ l.uri:scheme NE '//' }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '://' }" />
		</Mvif>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.uri:user AND NOT ISNULL l.uri:pass }">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.uri:user $ ':' $ l.uri:pass $ '@' }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.uri:subdomain }">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.uri:subdomain $ '.' }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.uri:domain }">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.uri:domain }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.uri:path }">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.uri:path }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.uri:query }">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '?' $ l.uri:query }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.uri:fragment }">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '#' $ l.uri:fragment }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URI_Add_Replace_Subdomain" PARAMETERS = "module var, param, uri, new_subdomain, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	URI_Add_Replace_Subdomain( uri, new_subdomain, output var )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ Parse_URI( l.module, l.param, l.uri, l.parsed_uri ) }">
		<MvASSIGN NAME = "l.parsed_uri" MEMBER = "subdomain" VALUE = "{ l.new_subdomain }" />
		<MvASSIGN NAME = "l.success" VALUE = "{ URI_Build_URI( l.module, l.param, l.parsed_uri, l.output) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URI_Add_Replace_UserPass" PARAMETERS = "module var, param, uri, user, pass, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	URI_Add_Replace_UserPass( uri, user, pass, output var )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ Parse_URI( l.module, l.param, l.uri, l.parsed_uri ) }">
		<MvASSIGN NAME = "l.parsed_uri" MEMBER = "user" VALUE = "{ l.user }" />
		<MvASSIGN NAME = "l.parsed_uri" MEMBER = "pass" VALUE = "{ l.pass }" />
		<MvASSIGN NAME = "l.success" VALUE = "{ URI_Build_URI( l.module, l.param, l.parsed_uri, l.output) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URI_Add_Replace_Scheme" PARAMETERS = "module var, param, uri, new_scheme, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	URI_Add_Replace_Scheme( uri, new_scheme, output var )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ Parse_URI( l.module, l.param, l.uri, l.parsed_uri ) }">
		<MvASSIGN NAME = "l.parsed_uri" MEMBER = "scheme" VALUE = "{ l.new_scheme }" />
		<MvASSIGN NAME = "l.success" VALUE = "{ URI_Build_URI( l.module, l.param, l.parsed_uri, l.output) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URI_Replace_TLD" PARAMETERS = "module var, param, uri, new_tld, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	URI_Replace_TLD( uri, new_tld, output var )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ Parse_URI( l.module, l.param, l.uri, l.parsed_uri ) }">
		<MvASSIGN NAME = "l.parsed_uri" MEMBER = "domain" VALUE = "{ glosub( l.parsed_uri:domain, '.' $ l.parsed_uri:tld, '.' $ l.new_tld) }" />
		<MvASSIGN NAME = "l.success" VALUE = "{ URI_Build_URI( l.module, l.param, l.parsed_uri, l.output) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URI_Replace_Hash" PARAMETERS = "module var, param, uri, new_hash, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	URI_Replace_Hash( uri, new_hash, output var )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ Parse_URI( l.module, l.param, l.uri, l.parsed_uri ) }">
		<MvASSIGN NAME = "l.parsed_uri" MEMBER = "fragment" VALUE = "{ l.new_hash }" />
		<MvASSIGN NAME = "l.success" VALUE = "{ URI_Build_URI( l.module, l.param, l.parsed_uri, l.output) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>