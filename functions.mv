<MvCOMMENT>
|
|	Custom Functions per Item
|
|	Function Setup:
|
|	<MvFUNCTION NAME = "XXXXX" PARAMETERS = "all_settings var, param var, parameters var, parameter_count" STANDARDOUTPUTLEVEL = "">
|
|
</MvCOMMENT>

<MvFUNCTION NAME = "Format_Currency" PARAMETERS = "all_settings var, param var, parameters var, parameter_count" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Format_Currency( plain_price, formatted_price var )
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.parameter_count NE 2 }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '2 Paramters are required for this function.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Is_Variable( l.parameters[2] ) }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'The second parameter must be a variable.' ) }">
	</MvIF>

	<MvDO NAME = "{ l.parameters[2] }" FILE = "{ g.Module_Root $ g.Store:currncy_mod:module }" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.parameters[1] ) }" />
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Upcharge" PARAMETERS = "all_settings var, param var, parameters var, parameter_count" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Upcharge( charge_name, charge_type, amount, tax_exempt )
	|
	</MvCOMMENT>
	<MvIF EXPR = "{ l.parameter_count NE 4 }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '4 Paramters are required for this function.' ) }">
	</MvIF>
	
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].BasketCharge_Delete_All_Type( g.Basket:basket_id, l.parameters[2] ) }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'An error occured' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.parameters[3] GT 0 }">
		<MvASSIGN NAME = "l.basket_charge:basket_id"	VALUE = "{ g.Basket:basket_id }">
		<MvASSIGN NAME = "l.basket_charge:type"			VALUE = "{ l.parameters[2] }">
		<MvASSIGN NAME = "l.basket_charge:descrip"		VALUE = "{ l.parameters[1] }">
		<MvASSIGN NAME = "l.basket_charge:amount"		VALUE = "{ l.parameters[3] }">
		<MvASSIGN NAME = "l.basket_charge:disp_amt"		VALUE = "{ l.parameters[3] }">
		<MvASSIGN NAME = "l.basket_charge:tax_exempt"	VALUE = "{ l.parameters[4] }">
		<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.sucess" VALUE = "{ BasketCharge_Insert( l.basket_charge ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Refresh the Basket Charges & Basket Total
	|
	</MvCOMMENT>

	<MvFOREACH ITERATOR = "l.charge" ARRAY = "l.charges" COUNT = "{ [ g.Module_Library_DB ].BasketChargeList_Load_Basket( g.Basket:basket_id, l.charges ) }">
		<MvDO NAME = "l.charge:formatted_amount" FILE = "{ g.Module_Root $ g.Store:currncy_mod:module }" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.charge:amount ) }" />
		<MvDO NAME = "l.charge:formatted_disp_amt" FILE = "{ g.Module_Root $ g.Store:currncy_mod:module }" VALUE = "{ CurrencyModule_AddFormatting( g.Store:currncy_mod, l.charge:disp_amt ) }" />
	</MvFOREACH>
	<MvASSIGN NAME = "l.all_settings:basket:charges" VALUE = "{ l.charges }">
	
	<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.all_settings:basket:total" VALUE = "{ Basket_Total( g.Basket:basket_id ) }">
	<MvDO FILE = "{ g.Module_Store_Module_Currency }" NAME = "l.all_settings:basket:formatted_total" VALUE = "{ CurrencyModule_AddFormatting( g.Store_Module_Currency, l.all_settings:basket:total ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>